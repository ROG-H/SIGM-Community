; Script generated by the HM NIS Edit Script Wizard.
SetCompressor lzma
RequestExecutionLevel user

; HM NIS Edit Wizard helper defines

!define PRODUCT_NAME "SIGEM Async Uploader"
#!define PRODUCT_VERSION "1.0.0" #<--- La versión se comenta para que se reciba como parámetro del script al invocarlo por línea de comandos: makensis.exe /X"!define PRODUCT_VERSION 1.0.0" script.nsi
!define PRODUCT_PUBLISHER "Deputación da Coruña"
!define PRODUCT_WEB_SITE "https://www.dicoruna.es/"

!define APP_NAME "addon-async-uploader"
!define APP_EXE "${APP_NAME}-${PRODUCT_VERSION}.exe"
!define APP_UNINST_EXE "Uninst_${APP_EXE}"

!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${APP_EXE}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!define MUI_WELCOMEPAGE_TITLE_3LINES
!insertmacro MUI_PAGE_WELCOME
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_TITLE_3LINES
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SpanishInternational"

; MUI end ------



Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup-${APP_NAME}-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\SIGEM\SIGEMAsyncUploader"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

#Primero se comprueba que no exista una versi�n previa instalada
Function .onInit
  ReadRegStr $R0 HKLM "${PRODUCT_UNINST_KEY}"  "UninstallString"
  StrCmp $R0 "" done
  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION  "Ya existe una versión previa instalada de ${PRODUCT_NAME}.$\n\
             Para instalar una nueva versión es necesario desinstalar primero la anterior.$\n$\n\
             Presione `Aceptar` para eliminar la versión anterior y luego proceder con la actualización o `Cancelar` para cancelar la actualización y no hacer nada."  IDOK uninst
  Abort

  ;Run the uninstaller
  uninst:
    ClearErrors
    ExecWait '$R0 _?=$INSTDIR' ;Do not copy the uninstaller to a temp file

    IfErrors no_remove_uninstaller done
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code
    ;here to remove the uninstaller. Use a registry key to check
    ;whether the user has chosen to uninstall. If you are using an uninstaller
    ;components page, make sure all sections are uninstalled.
    no_remove_uninstaller:

  done:
FunctionEnd


Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File ".\${APP_EXE}"
  CreateDirectory "$SMPROGRAMS\SIGEM"
  CreateShortCut "$SMPROGRAMS\SIGEM\${APP_NAME}-${PRODUCT_VERSION}.lnk" "$INSTDIR\${APP_EXE}" "" "$INSTDIR\Async-uploader.ico" 0
  CreateShortCut "$DESKTOP\${APP_NAME}-${PRODUCT_VERSION}.lnk" "$INSTDIR\${APP_EXE}" "" "$INSTDIR\Async-uploader.ico" 0
  #
  SetOverwrite try
  SetOutPath "$INSTDIR"
  File /r ".\resources\*"
SectionEnd

Section "sigem" SEC02
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  CreateShortCut "$SMPROGRAMS\SIGEM\Uninstall-${APP_NAME}-${PRODUCT_VERSION}.lnk" "$INSTDIR\Uninstall.exe"
SectionEnd

Section -Post
  # Insertar claves de registro de la aplicación
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\${APP_EXE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\Uninstall.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\${APP_EXE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  # Insertar claves de registro para el lanzamiento por protocolo
  WriteRegStr HKCR "sigemAsyncUploader" "" ""
  WriteRegStr HKCR "sigemAsyncUploader" "" "URL:Lanzador de editor online"
  WriteRegStr HKCR "sigemAsyncUploader" "URL Protocol" ""
  WriteRegStr HKCR "sigemAsyncUploader\DefaultIcon" "" ""
  WriteRegStr HKCR "sigemAsyncUploader\DefaultIcon" "" "${APP_EXE},1"
  WriteRegStr HKCR "sigemAsyncUploader\Shell\open\command" "" ""
  WriteRegStr HKCR "sigemAsyncUploader\Shell\open\command" "" "$\"$INSTDIR\${APP_EXE}$\" $\"%1$\""
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) fue desinsalado de su equipo con éxito."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "¿Está seguro que desea desintalar completamente $(^Name) y todos sus componentes?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  #Borrar accesos directos
  Delete "$SMPROGRAMS\SIGEM\Uninstall-${APP_NAME}-${PRODUCT_VERSION}.lnk"
  Delete "$DESKTOP\${APP_NAME}-${PRODUCT_VERSION}.lnk"
  Delete "$SMPROGRAMS\SIGEM\${APP_NAME}-${PRODUCT_VERSION}.lnk"

  # Borrar directorios del grupo de instalación  
  RMDir /r "$INSTDIR"

  # Borrar claves de registro
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCR "sigemAsyncUploader"
  SetAutoClose true
SectionEnd